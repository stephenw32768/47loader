#summary Unusual screen-loading routines.

= Introduction =

The 47loader-tzx tool can produce screens that load in unusual ways.  Such screens can be loaded by 47loader using an optional module, "dynamic". This module reads a table of block addresses, lengths and directions, then uses "resume" to load them in sequence.

= Creating a tape file =

You can use 47loader-tzx's `-fancyscreen` option to specify a fancy screen load.  The option takes as an argument one of the following:

|| *`-fancyscreen` argument* || *Effect* || *Loader direction after the load is complete* ||
|| `bidi_acp` || Attributes that start at the top and bottom and converge in the centre, then a bidirectional pixmap load. || Backwards ||
|| `bidi_ap` || Bidirectional screen load, attributes first. || Backwards ||
|| `bidi_pa` || Bidirectional screen load, pixmap first. || Backwards ||
|| `bidi_pac` || Bidirectional pixmap load followed by attributes that start at the top and bottom and converge in the centre. || Backwards ||
|| `btt_fa` || Loads the pixmap bottom-to-top instead of in thirds, then the attributes forwards. || Forwards ||
|| `btt_ra` || Loads the pixmap bottom-to-top instead of in thirds, then the attributes backwards. || Backwards ||
|| `fa_btt` || Loads the attributes forwards, then the pixmap bottom-to-top instead of in thirds. || Backwards ||
|| `fa_rp` || Loads the attributes forwards, then the pixmap backwards. || Backwards ||
|| `fa_ttb` || Loads the attributes forwards, then the pixmap top-to-bottom instead of in thirds. || Forwards ||
|| `fp_ra` || Loads the pixmap forwards, then the attributes backwards. || Backwards ||
|| `ra_btt` || Loads the attributes backwards, then the pixmap bottom-to-top instead of in thirds. || Backwards ||
|| `ra_fp` || Loads the attributes backwards, then the pixmap forwards. || Forwards ||
|| `ra_ttb` || Loads the attributes backwards, then the pixmap top-to-bottom instead of in thirds. || Forwards ||
|| `rp_fa` || Loads the pixmap backwards, then the attributes forwards. || Forwards ||
|| `ttb_fa` || Loads the pixmap top-to-bottom instead of in thirds, then the attributes forwards. || Forwards ||
|| `ttb_ra` || Loads the pixmap top-to-bottom instead of in thirds, then the attributes backwards. || Backwards ||

For example, this sequence of commands creates a tape file containing:
  * a screen that loads the pixmap forwards but the attributes backwards;
  * a block containing a game, immediately afterwards with no gap, for loading using "[Resume resume]":

{{{
47loader-tzx -fancyscreen fp_ra -pause 0 -output mygame.tzx mygame.scr
47loader-tzx -pilot resume -output mygame.tzx mygame.bin
}}}

When you use `-fancyscreen`, 47loader-tzx reports the length of the table containing the addresses, lengths and directions of each block comprising the screen.

= Writing the loader =

Fancy screens are loaded using a module called "dynamic" that exports an entry point, `loader_dynamic`.

You will need to define some symbols:

  * `LOADER_DYNAMIC_TABLE_ADDR`: as described above, `-fancyscreen` instructs 47loader-tzx to output a block containing a table describing the blocks that comprise the screen.  The value of this symbol is the memory address at which the table will be loaded.  There must be enough free memory at this address to store the number of bytes reported by 47loader-tzx.
  * `LOADER_RESUME`: "dynamic" requires "resume", so you must define this symbol.
  * `LOADER_DYNAMIC_FORWARDS_ONLY`: define this symbol if 47loader-tzx tells you to.  It disables the direction-change code in the "dynamic" module, saving a few bytes if the fancy screen effect does not load any blocks backwards.  If this symbol is not defined, direction changes are needed so you must define `LOADER_CHANGE_DIRECTION` to bring the required routine into the loader.
  * `LOADER_DYNAMIC_ONE_BYTE_LENGTHS`: define this symbol if 47loader-tzx tells you to.  If defined, the block lengths in the dynamic table are one byte wide.  47loader-tzx writes dynamic tables like this if no dynamic block is longer than 127 bytes.

Because fancy screens generally involve one or more changes of load direction, it is possible that the loader will be left pointing backwards after the screen has loaded.  The table above indicates which fancy effects this applies to.  Following a fancy screen load, you probably want to load the game forwards, so you must include a call to `loader_change_direction` before loading the game.

Here is an example of a loader that will be embedded in a BASIC REM statement.  It uses the "dynamic" module to load the fancy screen, then loads the game using "resume".

{{{
;;; relocate the loader to this address
LOADER_ABSOLUTE_ADDR:       equ     32768
        include "47loader_simple_basic_embed_top.asm"

;;;  this is the border theme to use.  A full list of the
;;;  available themes is on the Wiki:
;;;  https://code.google.com/p/47loader/wiki/BorderThemes
LOADER_THEME_RAINBOW:   equ 1

;;; this is the address at which the dynamic table will be
;;; loaded.  It happens to be the same address at which the
;;; game is to be loaded; once the screen in place, the table
;;; is not required any more
LOADER_DYNAMIC_TABLE_ADDR:equ 49152

;;; loader_dynamic requires the following options
LOADER_RESUME:equ 1
LOADER_CHANGE_DIRECTION:equ 1

        ;; loading the screen is a simple as just calling
        ;; loader_dynamic
        call    loader_dynamic
        ;; as with all 47loader entry points, the carry flag
        ;; is set if the load succeeded
        jr      nc,.err

        ;; the -fancyscreen option passed to 47loader-tzx in the
        ;; previous section specified the fp_ra effect.  This
        ;; effect leaves the loader pointing backwards, so we
        ;; need to change direction before loading the game
        call    loader_change_direction

        ;; load 8144 bytes at address 49152 using "resume"
        ld      ix,49152
        ld      de,8144
        call    loader_resume

        ;; if the load succeeded, jump to the game, otherwise
        ;; fall through to the error handler
        jp      c,49152

.err:
        include "47loader_error_handler.asm"
        include "47loader.asm"
        include "47loader_dynamic.asm"

        include "47loader_simple_basic_embed_bottom.asm"
}}}